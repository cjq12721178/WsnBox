apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.0"
    defaultConfig {
        applicationId "com.weisi.tool.wsnbox"
        minSdkVersion 18
        targetSdkVersion 27
        versionCode 23
        versionName "1.5.3"
        flavorDimensions "wsn"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    productFlavors {
        general {
        }
        guomi {
            applicationIdSuffix ".guomi"
            versionCode 19
            versionName "0.6.4"
        }
        jiugang {
            applicationIdSuffix ".jiugang"
            versionCode 6
            versionName "0.1.5"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                    appName: "@string/app_name_debug"
            ]
        }
        release {
            manifestPlaceholders = [
                    appName: "@string/app_name"
            ]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //打包后应用名称
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def fileName
            if (outputFileName != null && outputFileName.endsWith('.apk')) {
                if (variant.buildType.name.equals('release')) {
                    fileName = "${variant.applicationId}_${variant.versionName}_${buildTime()}.apk"
                } else if (variant.buildType.name.equals('debug')) {
                    fileName = "debug_${variant.applicationId}_${variant.versionName}.apk"
                }
                outputFileName = fileName
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    google()
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile(name: 'com.cjq.tool.qbox-1.6.0', ext: 'aar')
    compile(name: 'com.cjq.lib.weisi-5.0.4', ext: 'aar')
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:recyclerview-v7:27.0.2'
    compile 'com.android.support:preference-v7:27.0.2'
    compile 'com.kaopiz:kprogresshud:1.0.2'
    compile 'io.apisense:rhino-android:1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    implementation 'pub.devrel:easypermissions:1.1.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'com.android.support:cardview-v7:27.0.2'
}

apply plugin: 'kotlin-android-extensions'